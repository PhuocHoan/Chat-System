plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

group 'system'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.2'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'com.haichutieu.chatsystem.server'
    mainClass = 'com.haichutieu.chatsystem.server.ServerApp'
}


javafx {
    version = '21.0.5'
    modules = ['javafx.controls', 'javafx.fxml']
}

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    // Hibernate
    implementation enforcedPlatform("org.hibernate.orm:hibernate-platform:6.6.3.Final")
    implementation "org.hibernate.orm:hibernate-core"
    implementation "jakarta.transaction:jakarta.transaction-api"

    implementation 'org.postgresql:postgresql:42.7.4' // postgresql driver

    implementation 'org.mindrot:jbcrypt:0.4' // hash password by bcrypt
    implementation 'org.hibernate.orm:hibernate-hikaricp:6.6.3.Final' // hibernate connection pool by HikariCP

    // create log
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:slf4j-simple:2.0.16'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.2' // json mapper

    implementation 'com.sun.mail:javax.mail:1.6.2'
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
}

jlinkZip {
    group = 'distribution'
}

//// Task to create JAR for AdminClient
task adminJar(type: Jar) {
    group = "build"
    description = "Create JAR for AdminClient"
    archiveClassifier = 'admin'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'com.haichutieu.chatsystem.client.AdminApp' // Entry point for AdminClient
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } // Include dependencies
    }
    with jar
}

// Task to create JAR for UserClient
task userJar(type: Jar) {
    group = "build"
    description = "Create JAR for UserClient"
    archiveClassifier = 'user'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'com.haichutieu.chatsystem.client.ClientApp' // Entry point for UserClient
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } // Include dependencies
    }
    with jar
}

// Task to create JAR for ServerApp
task serverJar(type: Jar) {
    group = "build"
    description = "Create JAR for ServerApp"
    archiveClassifier = 'server'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Main-Class': 'com.haichutieu.chatsystem.server.ServerApp' // Entry point for ServerApp
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } // Include dependencies
    }
    with jar
}

// Ensure the JARs are built as part of the 'build' lifecycle
build.dependsOn adminJar, userJar, serverJar